/*
Contains tasks and setup to use for docs generation

Requires in the buildscript

- classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
- project `docs-sources` that contains under `src/main/asciidoc` the asciidoc
*/
buildscript {
	repositories { jcenter() }

	dependencies {
		classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorGradleVersion}"
		classpath "com.github.jruby-gradle:jruby-gradle-plugin:${jrubyVersion}"
		classpath 'org.ajoberstar:grgit:2.2.1'
	}
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'java'
apply plugin: "com.github.jruby-gradle.base"
apply plugin: 'org.asciidoctor.convert'

ext {
	git = Grgit.open()
	ghOrg = "CloudPipelines"
	repo = project.name
	branch = git.branch.current().getName()
	docsBaseUrl = [
		'https://raw.githubusercontent.com',
		ghOrg,
		repo,
		branch
	].join('/')
	scriptsBranch = "master"
	scriptsBaseUrl = [
		'https://raw.githubusercontent.com',
		ghOrg,
		"scripts",
		scriptsBranch
	].join('/')
}

task cleanDocs(type: Delete) {
	delete project(":docs").projectDir.path
}

task createFolder() {
	doLast {
		new File(project.rootDir, "docs").mkdirs()
	}
	mustRunAfter "cleanDocs"
}

dependencies {
	gems 'rubygems:asciidoctor-diagram:1.5.9'
}

asciidoctor {
	dependsOn = ["createFolder", "jrubyPrepare"]
	sourceDir = file('docs-sources/src/main/asciidoc')
	gemPath = jrubyPrepare.outputDir
	outputDir = file('docs')
	separateOutputDirs = false
	requires = ['asciidoctor-diagram']
	attributes 'source-highlighter': 'prettify',
		toc: 'left',
		toclevels: '4',
		'hide-uri-scheme': '',
		'numbered': '',
		'icons': 'font',
		idprefix: '',
		idseparator: '-',
		safe: 'unsafe',
		parse: 'false',
		'allow-uri-read': '',
		'scripts-base-url': scriptsBaseUrl,
		'intro-root-docs': "${scriptsBaseUrl}/docs/images/intro",
		'docs-base-url': docsBaseUrl,
		'demo-root-docs': "${docsBaseUrl}/docs/images/demo",
		'jenkins-root-docs' : "${docsBaseUrl}/docs/images/jenkins"
}

task generateDocs() {
	description = "Creates documentation for the project"
	dependsOn = ["cleanDocs", "asciidoctor"]
}
